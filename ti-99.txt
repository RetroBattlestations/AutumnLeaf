TO MAIN

TELL TURTLE
HIDETURTLE
CLEARSCREEN
COLORBACKGROUND :BLACK
SETCOLOR :RUST
PENUP
FORWARD 30
RIGHT 90
BACK 120
PENDOWN
FORWARD 30
BRANCH 40
MAKE "DUMMY READCHAR  ; WAIT FOR CHARACTER
COLORBACKGROUND :CYAN
END

; SET TURTLE POSITION AND HEADING
TO SETWHERE :W
SXY (FIRST :W) (FIRST BUTFIRST :W)
SETHEADING LAST :W
END

TO BRANCH :SIZE
IF :SIZE < 2 THEN STOP
LEFT 90
STEMLEAF :SIZE
RIGHT 92
FORWARD :SIZE
RIGHT 90
STEMLEAF :SIZE
LEFT 88
FORWARD :SIZE
BRANCH (:SIZE * 13 + 10) / 20
END

TO STEMLEAF :SIZE
FORWARD :SIZE / 4
LEFT 90
FORWARD (:SIZE * 12 + 25) / 53
BACK (:SIZE * 12 + 25) / 53 * 2
FORWARD (:SIZE * 12 + 25) / 53
RIGHT 90
PENUP
FORWARD (:SIZE * 12 + 38) / 77
LEAF :SIZE
BACK (:SIZE * 12 + 38) / 77
BACK :SIZE / 4
PENDOWN
END

TO LEAF :SIZE
MAKE "CW WHERE
LEAFP :SIZE
PENUP
LEFT 90
FORWARD (:SIZE * 3 + 10) / 20
LEFT 65
FORWARD (:SIZE * 9 + 50) / 100
RIGHT 90
LEAFP (:SIZE * 6 + 5) / 10
PENUP
SETWHERE :CW
RIGHT 90
FORWARD (:SIZE * 3 + 10) / 20
RIGHT 65
FORWARD (:SIZE * 9 + 50) / 100
LEFT 90
LEAFP (:SIZE * 6 + 5) / 10
SETWHERE :CW
END

TO LEAFP :SIZE
PENUP
LEFT 90
FORWARD (:SIZE * 3 + 10) / 20
PENDOWN
RIGHT 70
FORWARD (:SIZE * 47 + 39) / 71
RIGHT 65
FORWARD (:SIZE * 41 + 41) / 77
PENUP
RIGHT 45 + 90
FORWARD :SIZE
LEFT 90
FORWARD (:SIZE * 3 + 10) / 20
PENDOWN
LEFT 70
FORWARD (:SIZE * 47 + 39) / 71
LEFT 65
FORWARD (:SIZE * 41 + 41) / 77
PENUP
LEFT 45 + 90
FORWARD :SIZE
LEFT 180
END

MAIN
; PRESS FCTN-9
